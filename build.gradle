buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'spring-boot'

sourceCompatibility = 1.8

jar {
    baseName = 'spring-boot-standalone'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
    // Spock snapshots are available from the Sonatype OSS snapshot repository
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    String springBootVersion = "1.2.1.RELEASE"
    compile("org.springframework.boot:spring-boot-starter:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-logging:$springBootVersion")
    compile("org.codehaus.groovy:groovy-all:2.3.8")

    testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testRuntime("org.spockframework:spock-spring:0.7-groovy-2.0") {
        exclude group: 'org.spockframework', module: 'spock-core'
    }
    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
}

sourceSets {
    test {
        groovy {
            srcDirs = ['src/test/unit/groovy', 'src/test/integration/groovy']
        }
    }
}